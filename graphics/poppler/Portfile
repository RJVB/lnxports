# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:filetype=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           compiler_blacklist_versions 1.0
PortGroup           conflicts_build 1.0
PortGroup           gobject_introspection 1.0
PortGroup           cmake 1.1
platform darwin {
    if {${os.major} < 13} {
        PortGroup   legacysupport 1.0
    }
}
PortGroup           preserve_runtime_libraries 1.0

name                poppler
conflicts           xpdf-tools
version             0.73.0
license             GPL-2+
maintainers         {devans @dbevans} openmaintainer
categories          graphics
platforms           darwin
homepage            https://poppler.freedesktop.org/

description         Poppler is a PDF rendering library based on the xpdf-3.0 code base.
long_description    ${description}

master_sites        ${homepage}

use_xz              yes

# avoid certificate issues on freedesktop.org
fetch {
    if {![file exists ${distpath}/${distname}${extract.suffix}]} {
        system "wget ${master_sites}/${distname}${extract.suffix} --no-check-certificate --progress=bar:force \
            -O ${distpath}/${distname}${extract.suffix}"
    }
}

checksums           rmd160  9b3dae1912e29300b4149a71e6dc4b9f175be5b2 \
                    sha256  e44b5543903128884ba4538c2a97d3bcc8889e97ffacc4636112101f0238db03
#                     size    1507068

depends_build-append \
                    port:pkgconfig

depends_lib-append  port:bzip2 \
                    port:curl \
                    port:expat \
                    port:jpeg \
                    path:lib/pkgconfig/glib-2.0.pc:glib2 \
                    port:lcms2 \
                    port:libiconv \
                    port:libpng \
                    port:openjpeg \
                    port:poppler-data \
                    port:tiff \
                    port:zlib
# taken from the host:
# depends_lib-append  port:fontconfig \
#                     port:freetype \
#                     path:lib/pkgconfig/cairo.pc:cairo \
#                     port:openjpeg

configure.ldflags-append -liconv
gobject_introspection no
configure.args-append \
                    -DENABLE_GOBJECT_INTROSPECTION=OFF

# C++11 for thread_local (contrary to what the mainstream port claims)
configure.cxxflags-append -std=c++11
compiler.blacklist-append {clang < 800.0.38}
foreach clv {3.3 3.4 3.5 3.6 3.7 3.8 3.9 4.0} {
    compiler.blacklist macports-clang-${clv}
}
foreach clv {5.0 6.0 7.0} {
    if {[file exists ${prefix}/bin/clang-mp-${clv}]} {
        compiler.whitelist-prepend macports-clang-${clv}
    }
    compiler.fallback-prepend macports-clang-${clv}
}

# https://bugs.freedesktop.org/show_bug.cgi?id=106417
patchfiles-append   patch-bug106417.diff

# https://trac.macports.org/ticket/57167
patchfiles-append   patch-trac-57167.diff
  
configure.args-append \
                    -DENABLE_XPDF_HEADERS=ON \
                    -DENABLE_UNSTABLE_API_ABI_HEADERS=ON \
                    -DENABLE_QT5=OFF \
                    -DBUILD_GTK_TESTS=OFF \
                    -DBUILD_QT5_TESTS=OFF \
                    -DBUILD_CPP_TESTS=OFF
#                     -DWITH_NSS3=OFF

cmake.save_configure_cmd "log too"

subport poppler-qt5 {
    PortGroup qt5 1.0

    patchfiles-append       patch-qt5-106118.diff

    configure.env-append    MOCQT5=${qt_bins_dir}/moc
    configure.args-delete   -DENABLE_QT5=OFF
    configure.args-delete   -DENABLE_XPDF_HEADERS=ON
    configure.args-append   -DENABLE_UTILS=OFF
    configure.args-append   -DENABLE_CPP=OFF
    configure.args-append   -DENABLE_GLIB=OFF

    post-destroot {
        file delete ${destroot}${prefix}/lib/pkgconfig/poppler-cairo.pc
        if {[variant_isset preserve_runtime_libraries]} {
           if {![catch {set installed [lindex [registry_active ${subport}] 0]}]} {
                set _version [lindex $installed 1]
                # check if the (or a) previous port version is active
                if {[vercmp $_version ${version}] <= 0} {
                    # move new interface lib aside
                    file rename ${destroot}${prefix}/lib/libpoppler-qt5.so.1 \
                        ${destroot}${prefix}/lib/libpoppler-qt5.so.1n
                    # update link library
                    ln -sf libpoppler-qt5.so.1n ${destroot}${prefix}/lib/libpoppler-qt5.so
                    # install pointer to old runtime which had only a single version interfix
                    # so that dependents can continue to use that previous version.
                    preserve_libraries ${prefix}/lib libpoppler-qt5.so.*
                }
           }
        } else {
            # check if the user possibly was using +preserve_runtime_libraries before,
            # as evidenced by the presence of the renamed interface lib.
            if {[file exists ${prefix}/lib/libpoppler-qt5.so.1n]} {
                # make sure any applications currently depending on the renamed library
                # continue to work without rebuilding. We don't have to without the
                # +preserve_runtime_libraries variant set but adding a simple symlink
                # to what should be an ABI-compatible library is cheap and most likely safe.
                # (Apps will use the new version just like they would with a rebuild.)
                ln -sf libpoppler-qt5.so.1 ${destroot}${prefix}/lib/libpoppler-qt5.so.1n
            }
        }
    }
}

if {${subport} ne ${name}} {
    depends_extract-append  port:git

    depends_lib-append      port:poppler

    post-extract {
        system -W ${workpath} "${prefix}/bin/git clone --depth=1 http://anongit.freedesktop.org/git/poppler/test"
    }


# currently poppler only provides unit tests for the Qt wrappers

    test.run    yes
    test.dir    test
    test.target test

    post-destroot {
        foreach dot_h [glob -nocomplain -directory ${destroot}${prefix}/include/poppler *.h] {
            delete $dot_h
        }
        foreach libpoppler [glob -nocomplain ${destroot}${prefix}/lib/libpoppler.*] {
            delete ${libpoppler}
        }
        delete  ${destroot}${prefix}/include/poppler/fofi \
                ${destroot}${prefix}/include/poppler/goo \
                ${destroot}${prefix}/include/poppler/splash \
                ${destroot}${prefix}/lib/pkgconfig/poppler-splash.pc \
                ${destroot}${prefix}/lib/pkgconfig/poppler.pc
    }
} else {
    # generation of Poppler-0.18.gir fails if previous version of poppler is active
    # appropriate for main poppler port only
    if {[tbool gobject_introspection]} {
        conflicts_build ${name}
    }
    post-destroot {
        preserve_libraries ${prefix}/lib libpoppler.so.*
    }
}

livecheck.type      regex
livecheck.url       ${homepage}releases.html
livecheck.regex     "${name}-(\\d+(?:\\.\\d+)*)${extract.suffix}"
