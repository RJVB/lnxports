# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           locale_select 1.0
PortGroup           LTO 1.0
PortGroup           save_configure_cmd 1.0
PortGroup           conflicts_build 1.0
platform linux {
    PortGroup       preserve_runtime_libraries 1.0
}

name                binutils
version             2.42

checksums           rmd160  1aecf0d749c7eb0941f7e1f0be0006d8a8833dd8 \
                    sha256  f6e4d41fd5fc778b06b7891457b3620da5ecea1006c6a4a41ae998109f85a800
#                     size    27567160

description         FSF Binutils for native development.
long_description    Free Software Foundation development toolchain ("binutils") \
                    for native development. Tools are prefixed with g on Mac to avoid \
                    conflicts with original tools.

platforms           darwin linux
categories          devel
license             GPL-3+
maintainers         {@catap korins.ky:kirill} \
                    openmaintainer
homepage            https://www.gnu.org/software/binutils/binutils.html
master_sites        gnu:binutils
use_xz              yes

depends_build-append \
                    port:gettext \
                    port:gettext-dev

# Enforce systems nm on 10.10 and earlier,
# See: https://trac.macports.org/ticket/65621
if {${os.platform} eq "darwin" && ${os.major} <= 14} {
    build.env-append \
                    NM=/usr/bin/nm
    build.args-append \
                    lt_cv_path_NM=/usr/bin/nm
}

depends_lib-append  port:gettext-runtime \
                    port:libiconv \
                    port:zlib \
                    port:zstd

# --enable-shared leads to Undefined symbols, avoid it
# See: https://sourceware.org/bugzilla/show_bug.cgi?id=31056
configure.args      --enable-option-checking \
                    --infodir='${prefix}/share/info' \
                    --mandir='${prefix}/share/man' \
                    --enable-year2038 \
                    --disable-werror \
                    --enable-libssp \
                    --with-isl=${prefix}

if {${os.platform} eq "linux"} {
    ###############################
    PortGroup       devport 1.0
    create_devport  port:${name}

    if {![is_mainport]} {
        # nothing else to be done
        return
    }
    ###############################

    conflicts_build "${name}-dev"

    configure.args-append \
                    --enable-lto \
                    --enable-objc-gc \
                    --enable-host-shared \
                    --enable-compressed-debug-sections=all
    # --enable-shared is no longer available but the `enable_shared` variable still consulted in configure
    configure.env-append \
                    "enable_shared=yes" \
                    "LIBS=-L${prefix}/lib -lintl"
    build.env-append \
                    "LIBS=-L${prefix}/lib -lintl"
} else {
    configure.args-append \
                    --program-prefix=g \
}

configure.save_configure_cmd "install log"

# The Makefile runs configure again in subdirectories.
# It correctly passes along most configure variables (CFLAGS, LDFLAGS, ...),
#    but seems to neglect CPPFLAGS.
build.env-append    "CPPFLAGS=-I${prefix}/include"

# Binutils violates the mtree layout by creating ${prefix}/<arch>/
destroot.violate_mtree  yes

# We don't want the etc module.
# But leave texi2pod.pl untouched for processing man pages
platform darwin {
    post-extract {
        move ${worksrcpath}/etc/texi2pod.pl ${worksrcpath}
        delete {*}[glob ${worksrcpath}/etc/*]
        move ${worksrcpath}/texi2pod.pl ${worksrcpath}/etc/texi2pod.pl
    }
    notes "Having ${name} installed will cause some other ports to\
    fail to build. Consider uninstalling ${name}."
}

universal_variant   no

platform linux {
    post-destroot {
        set archdir "${prefix}/${build_arch}-pc-linux-gnu"
        # the MacPorts install/activation process breaks hard symlinks
        file delete ${destroot}${archdir}/bin/ld.bfd
        ln -s ld ${destroot}${archdir}/bin/ld.bfd
        foreach bin [glob ${destroot}${prefix}/bin/*] {
            set b [file tail ${bin}]
            if {[file exists ${destroot}${archdir}/bin/${b}]} {
                file delete -force ${bin}
                ln -s ${archdir}/bin/${b} ${destroot}${prefix}/bin/
            }
        }

        register_devport_standard_content
        create_devport_content_archive
        # now preserve the shared libraries installed by previous version(s) of the port.
        # These aren't versioned so devport left them alone in those versions because they
        # were regular files. Preserving them turns them into symlinks and devport would
        # thus move them if we did the step before populating  the devport.
        preserve_libraries ${prefix}/lib {libbfd*.so libopcodes*.so}
    }
}

