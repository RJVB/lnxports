# -*- coding: utf-8; mode: tcl; tab-width: 4; truncate-lines: t; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:et:sw=4:ts=4:sts=4

PortSystem          1.0

PortGroup           github 1.0
PortGroup           wxWidgets 1.0
PortGroup           conflicts_build 1.0
PortGroup           active_variants 1.1

platform darwin {
    if {${os.major} < 13} {
        PortGroup   cxx11 1.1
    }
}

name                audacity
conflicts           audacity-gtk

description         A Free, Cross-Platform Digital Audio Editor.
long_description    Audacity is a free, easy-to-use, multi-track audio editor and recorder \
                    for Windows, Mac OS X, GNU/Linux and other operating systems. The interface is translated \
                    into many languages. You can use Audacity to: \n\
                    Record live audio. \n\
                    Record computer playback on any Windows Vista or later machine. \n\
                    Convert tapes and records into digital recordings or CDs. \n\
                    Edit WAV, AIFF, FLAC, MP2, MP3 or Ogg Vorbis sound files. \n\
                    AC3, M4A/M4R (AAC), WMA and other formats supported using optional libraries. \n\
                    Cut, copy, splice or mix sounds together. \n\
                    Numerous effects including change the speed or pitch of a recording. \n\
                    And more!

subport             audacity-devel {
    long_description A port of the Audacity audio editor aimed at development purposes.
}


if {${subport} eq "${name}"} {
    conflicts       "${name}-devel"
    # get the source tarball from github because it contains all required external libs
    # incl. those not in MacPorts.
    #github.setup    audacity audacity 2.3.0 Audacity-
    # exceptionally we ship the same version as the devel port
    github.setup    audacity audacity 660b8c07ad63c83dc5f9d34f359bd4480e43f9eb
    version         2.3.2.361
    checksums       rmd160  00805ebb7d2a1cfb8685008c10fe1522b9e1b538 \
                    sha256  a9f9c70578444b1aa120f5e29a87f0f2bd1274dca145daa2119b761002ac9604
#                     size    62025555
    set PPREFIX     ./
} else {
    conflicts       "${name}"
    # git describe --tags : Audacity-2.3.2-361-g660b8c07a
    github.setup    audacity audacity 660b8c07ad63c83dc5f9d34f359bd4480e43f9eb
    version         2.3.2.361
    # this port is intended to facilitate development efforts shared with "upstreams"
    # so using git instead of a tarball fetch type is more appropriate.
    fetch.type      git
    distname        audacity-git
    set PPREFIX     devel/

    livecheck.type  none

    pre-build {
        # register the exact source version in the build:
        system -W ${worksrcpath}/mac "scripts/get_gitident.sh"
    }
    build.post_args-append -k
}

categories          audio

platforms           darwin
license             GPL-2+
maintainers         gmail.com:rjvbertin openmaintainer
universal_variant   no
use_parallel_build  yes

homepage            http://www.audacityteam.org/

# we want to use the wxWidgets-3.2 "bleeding edge" port!
wxWidgets.use       wxWidgets-3.2

depends_lib-append  port:${wxWidgets.port}

if {![catch {set wxw_stdlib [active_variants ${wxWidgets.port} stdlib]} err]} {
    if {${wxw_stdlib}} {
        long_description-append "This port needs ${wxWidgets.port} without the +stdlib variant."
        pre-fetch {
            ui_error "This port needs ${wxWidgets.port} without the +stdlib variant."
            return -code error "reinstall port:${wxWidgets.port} without the +stdlib variant"
        }
    }
}

# if {[variant_exists wx32] && [variant_isset wx32]} {
#     variant qt description {require wxWidgets-3.2 +qt (experimental!!)} {}
#     if {[variant_isset qt]} {
#         set qt_ok 0
#         if {![catch {set wxw_qt [active_variants ${wxWidgets.port} stdlib]} err]} {
#             if {${wxw_qt}} {
#                 set qt_ok 1
#             }
#         }
#         if {!${qt_ok}} {
#             long_description-append "This port needs ${wxWidgets.port} with the +qt variant."
#             pre-fetch {
#                 ui_error "This port needs ${wxWidgets.port} with the +qt variant."
#                 return -code error "reinstall port:${wxWidgets.port} with the +qt variant"
#             }
#         }
#     }
# }

depends_build-append \
                    port:pkgconfig \
                    port:python27
# depends_lib-append  port:freetype \
#                     port:harfbuzz \
#                     port:libid3tag \
#                     port:libmad \
#                     port:soundtouch \
#                     port:soxr \
#                     port:twolame
depends_lib-append  port:expat \
                    path:lib/libavcodec.dylib:ffmpeg \
                    port:flac \
                    port:libogg \
                    port:libsndfile \
                    port:libvorbis
# audacity could use port:lv2 and port:lilv but would also require a port:suil for that to be possible.

patch.pre_args      -Np1

# address issues in the build system:
# - build and link VSTControlOSX.mm instead of the GTk interface
# - address a visibility issue with the slider_array variable in libnyquist
# - make the AudioUnits plugin support build; on 64bit and also include AUControl.mm
platform darwin {
    patchfiles-append \
                    fix-minsrc-autoreconf.patch                             \
                    libvamp-Makefile-for-osx.diff                           \
                    src-Makefile-for-osx.diff                               \
                    portaudio-no-universal-build.diff                       \
                    buildinfo-clarify-no-gstreamer.diff                     \
                    patch-aboutdlg-compiler.diff                            \
                    add_enGB_translation.diff                               \
                    patch-more-decent-font-sizes.diff                       \
                    patch-python.diff                                       \
                    patch-libnyquist-symbol-visibility.diff \
                    patch-skip-gcc-version-test.diff \
                    patch-fix-audiounits.diff
    # reported upstream, patch accepted as the proper fix:
    patchfiles-append \
                    patch-unordered_map-fixes.diff
    # for building against wxWidgets 3.0.3 and up:
    patchfiles-append \
                    patch-wxw-303.diff
    # work around a missing implementation in wxCocoa's wxSound class
    patchfiles-append \
                    patch-implement-progdial=beep.diff
    patchfiles-append \
                    patch-no-libatomic.diff \
                    patch-WindowMenus-objcxx.diff
}
platform linux {
#     # this is likely to be true as long as we use the host's wxWidgets:
#     conflicts_build-append \
#                     tiff-dev
    patchfiles-append \
                    fix-minsrc-autoreconf.patch                             \
                    libvamp-Makefile-for-linux.diff                         \
                    buildinfo-clarify-no-gstreamer.diff                     \
                    patch-aboutdlg-compiler.diff                            \
                    add_enGB_translation.diff                               \
                    patch-avoid-clang-choke-on-confbase.diff                \
                    patch-rearrange-control-buttons.diff                    \
                    patch-python-linux.diff                                 \
                    patch-libnyquist-symbol-visibility.diff \
                    patch-skip-gcc-version-test.diff \
                    patch-fix-audiounits.diff
    # strange C++ errors, why would upstream builds not be affected?
#     patchfiles-append \
#                     patch-unordered_map-fixes.diff
    # for building against wxWidgets 3.0.3:
    patchfiles-append \
                    patch-wxw-303.diff
#     set wxWidgets.wxconfig /usr/bin/wx-config
}
patchfiles-append   patch-wxw-31x.diff

configure.args-append \
                    --disable-dependency-tracking \
                    --disable-quicktime \
                    --disable-static \
                    --disable-universal_binary \
                    --disable-audiounits \
                    --enable-shared \
                    --enable-sse \
                    --with-ffmpeg \
                    --with-lame \
                    --with-libflac \
                    --with-libmad \
                    --with-libsoxr \
                    --with-libvamp=local \
                    --with-libvorbis \
                    --with-lv2=local \
                    --with-portmixer \
                    --with-sbsms=local \
                    --with-soundtouch \
                    --with-twolame \
                    --with-widgetextra=local \
                    --with-portmidi=system \
                    CPPFLAGS=-I${prefix}/include \
                    WX_CONFIG=${wxWidgets.wxconfig}
platform darwin {
    # Since version 2.2.0 we need to use the embedded (modified) Portaudio version.
    configure.args-append \
                    --with-portaudio=local
}
configure.env-append \
                    PORTMIDI_CFLAGS=-I${prefix}/include \
                    PORTMIDI_LIBS=${prefix}/lib/libportmidi.dylib
depends_lib-append \
                    port:portmidi
platform linux {
    if {[string match *clang* ${configure.cxx}]} {
        variant libcxx description {highly experimental and probably broken option to build against libc++. \
                Requires using clang and an independently provided libc++ installation.} {
            configure.cxx_stdlib \
                            libc++
        }
    }

#     configure.args-append \
#                     --with-midi \
#                     LIBS=-ltiff
    configure.args-append \
                    --with-midi \
                    --with-portaudio=local
    configure.cppflags-append \
                    -I${prefix}/include
    configure.cflags-append \
                    -DUSE_AQUA_THEME
    configure.cxxflags-append \
                    -DUSE_AQUA_THEME \
                    -std=gnu++11
#     # as long as we use the host's wxWidgets. We use the static library
#     # because otherwise we might still be getting MacPorts' libtiff
#     # which lacks the version symbols required by the host's wxWidgets libraries.
#     configure.ldflags-append \
#                     /usr/lib/x86_64-linux-gnu/libtiff.a
    configure.args-replace \
                    CPPFLAGS=-I${prefix}/include \
                    CPPFLAGS=-I${prefix}/include -DUSE_AQUA_THEME
    configure.env-replace \
                    PORTMIDI_LIBS=${prefix}/lib/libportmidi.dylib \
                    PORTMIDI_LIBS=${prefix}/lib/libportmidi.so
}

# make sure the C++ stdlib argument is added to the LDFLAGS too
if {${configure.cxx_stdlib} ne "" && [string match *clang* ${configure.cxx}]} {
    configure.ldflags-append \
                    -stdlib=${configure.cxx_stdlib}
}

build.args-append   V=1 VERBOSE=1

# with everything defined/declared as intended we can now
# handle the post-patch step with its reinplace statements.
post-patch {
    set python_bin ${prefix}/bin/python2.7
    reinplace -W ${worksrcpath} "s|@@PYTHON@@|${python_bin}|g" \
                                lib-src/lv2/build \
                                lib-src/lv2/configure
    reinplace -W ${worksrcpath} "s|/usr/local|${prefix}|g" \
                                src/effects/ladspa/LadspaEffect.cpp \
                                src/effects/VST/VSTEffect.cpp \
                                src/export/ExportMP3.cpp \
                                lib-src/lv2/lilv/wscript \
                                lib-src/lv2/lilv/test/lilv_test.c
    reinplace -W ${worksrcpath} "s|/Library/Audio/Plug-Ins/Vamp|/Library/Audio/Plug-Ins/Vamp:${prefix}/lib/vamp|g" \
                                lib-src/libvamp/src/vamp-hostsdk/PluginHostAdapter.cpp
    reinplace -W ${worksrcpath} "s|/Library/Application Support/audacity/libs|${prefix}/lib|g" \
                                src/FFmpeg.h
    reinplace -W ${worksrcpath} "s| -Werror||g" \
                                lib-src/portaudio-v19/configure.in \
                                lib-src/portaudio-v19/configure
    reinplace -W ${worksrcpath} "s|@LDFLAGS@|${configure.ldflags}|g" \
                                lib-src/libvamp/Makefile.in
    platform darwin {
        # audacity 2.1.2 contains C++ files that include system SDK headers and ObjC code on OS X;
        # they must thus be built as ObjC++
        set make_objcpp { \
            {CommandManager src/commands    {src/Makefile.in src/Makefile.am}} \
            {Effect         src/effects     {src/Makefile.in src/Makefile.am}} \
            {LV2Effect      src/effects/lv2 {src/Makefile.in src/Makefile.am}} \
        }
        foreach mm ${make_objcpp} {
            set f  [lindex ${mm} 0]
            set d  [lindex ${mm} 1]
            set mk [lindex ${mm} 2]
            foreach m ${mk} {
                reinplace -W ${worksrcpath} "s|/${f}.cpp|/${f}.mm|g" ${m}
            }
            # it's easiest to symlink f.mm to f.cpp; clang++ will do the rest.
            if {![file exists ${worksrcpath}/${d}/${f}.mm]} {
                ln -s ${f}.cpp ${worksrcpath}/${d}/${f}.mm
            }
        }
        reinplace -W ${worksrcpath} "s|/VSTControlGTK.cpp|/VSTControlOSX.mm|g" \
                src/Makefile.in src/Makefile.am
        if {[file exists ${worksrcpath}/src/menus/WindowMenus.cpp] && ![file exists ${worksrcpath}/src/menus/WindowMenus.mm]} {
            ln -s WindowMenus.cpp ${worksrcpath}/src/menus/WindowMenus.mm
        }
    }
}

platform darwin {
    set aud_app_path    ${applications_dir}/Audacity.app
    destroot.keepdirs   ${destroot}${aud_app_path}/Contents/Resources/en.lproj

    post-destroot {
        # create the app bundle infrastructure
        xinstall -m 755 -d ${destroot}${aud_app_path}/Contents/MacOS
        xinstall -m 755 -d ${destroot}${aud_app_path}/Contents/Resources
        # the BundleExec:
        file rename ${destroot}${prefix}/bin/audacity ${destroot}${aud_app_path}/Contents/MacOS/Audacity
        # install the Info.plist provided by Audacity
        xinstall -m 644 ${worksrcpath}/mac/Info.plist ${destroot}${aud_app_path}/Contents/
        # adapt the provided Info.plist
        reinplace "s|AUDACITY_EXECUTABLE|Audacity|g" ${destroot}${aud_app_path}/Contents/Info.plist
        reinplace "s|AUDACITY_INFO_VERSION|${version} (MacPorts)|g" ${destroot}${aud_app_path}/Contents/Info.plist
        # install the provided icons into the Resources directory
        foreach r [glob -nocomplain ${worksrcpath}/mac/Resources/*.icns] {
            xinstall -m 644 ${r} ${destroot}${aud_app_path}/Contents/Resources/
        }
        # allow the application to find certain things where it expects them on OS X (= inside the app bundle)
        foreach r [glob -nocomplain ${destroot}${prefix}/share/audacity/*] {
            ln -s ${prefix}/share/audacity/[file tail ${r}] ${destroot}${aud_app_path}/Contents/Resources
        }
        # install translation resources
        foreach l [glob -nocomplain ${destroot}${prefix}/share/locale/*] {
            set lang [file tail ${l}]
            xinstall -m 755 -d ${destroot}${aud_app_path}/Contents/Resources/${lang}.lproj
            ln -s ${prefix}/share/locale/${lang}/LC_MESSAGES/audacity.mo ${destroot}${aud_app_path}/Contents/Resources/${lang}.lproj/
        }
        foreach l [glob -nocomplain ${wxWidgets.prefix}/share/locale/*] {
            set lang [file tail ${l}]
            xinstall -m 755 -d ${destroot}${aud_app_path}/Contents/Resources/${lang}.lproj
            ln -s ${l}/LC_MESSAGES/wxstd.mo ${destroot}${aud_app_path}/Contents/Resources/${lang}.lproj/
        }
        # install a wrapper script in ${prefix}/bin
        system "echo \"#!/bin/sh\nexec \\\"${aud_app_path}/Contents/MacOS/Audacity\\\" \\\"\\\$\@\\\"\" > ${destroot}${prefix}/bin/audacity"
        system "chmod 755 ${destroot}${prefix}/bin/audacity"
    }
}

notes "
    In case Audacity complains about finding FFmpeg or Lame, please locate these libraries\
    under ${prefix}/lib in the Libraries section of the Preferences dialog (see\
    http://manual.audacityteam.org/man/installing_and_updating_audacity_on_linux.html#locate)
"

# kate: backspace-indents true; indent-pasted-text true; indent-width 4; keep-extra-spaces true; remove-trailing-spaces modified; replace-tabs true; replace-tabs-save true; syntax Tcl/Tk; tab-indents true; tab-width 4;
