# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem 1.0
PortGroup select 1.0
PortGroup conflicts_build 1.0
PortGroup openssl 1.0

set LTO.configure_option --with-lto
PortGroup LTO 1.0
PortGroup save_configure_cmd 1.0

name                python39

# Remember to keep py39-tkinter and py39-gdbm's versions sync'd with this
version             3.9.19

set branch          [join [lrange [split ${version} .] 0 1] .]
categories          lang
license             PSF
maintainers         {jmr @jmroot}

description         An interpreted, object-oriented programming language
long_description    Python is an interpreted, interactive, object-oriented \
                    programming language.

homepage            https://www.python.org/
master_sites        ${homepage}ftp/python/${version}/

platforms           linux darwin

distname            Python-${version}
use_xz              yes
checksums           md5 87d0f8281237b972ff8b23e0e2c8d325 \
                    rmd160 b46fbe50a0cace4a6ecdb70d026a32932427f566 \
                    sha256 d4892cd1618f6458cb851208c030df1482779609d0f3939991bd38184f8c679e

patchfiles          patch-setup.py.diff \
                    patch-Lib-cgi.py.diff \
                    patch-configure.diff \
                    patch-Lib-ctypes-macholib-dyld.py.diff \
                    patch-libedit.diff \
                    patch-configure-xcode4bug.diff \
                    sysconfig.py.diff \
                    libedit-types.patch

if {${os.platform} eq "darwin" && ${os.major} <= 10} {
    # work around no copyfile and/or pthread_threadid_np on older systems
    patchfiles-append  patch-no-copyfile-on-Tiger.diff \
                       patch-threadid-older-systems.diff
}

depends_build       port:pkgconfig \
                    port:ncurses-dev \
                    port:gettext-dev \
                    port:ncurses-dev \
                    port:patchelf

depends_lib         port:bzip2 \
                    port:expat \
                    port:gettext-runtime \
                    port:libedit \
                    port:libffi \
                    port:ncurses \
                    port:sqlite3 \
                    port:xz \
                    port:zlib
depends_run         port:python_select \
                    port:python3_select

# blacklist llvm-gcc-4.2 compiler known to produce bad code
compiler.blacklist-append *llvm-gcc-4.2
if {${os.platform} ne "darwin"} {
    # to prevent `conflicts_build     ${subport} py39-setuptools` :
    configure.cppflags-delete -I${prefix}/include
}

# ensurepip arg may be removed later, now conflicts with pip and setuptools
# packages
configure.args      [expr {${os.platform} eq "darwin" ? "--enable-framework=${frameworks_dir}" : "--enable-shared"}] \
                    --enable-ipv6 \
                    --enable-loadable-sqlite-extensions \
                    --with-computed-gotos \
                    --with-ensurepip=no \
                    --with-system-expat \
                    --with-system-ffi

configure.ccache    no
# pkg-config removes -I flags for paths in CPATH, which confuses python.
configure.env       PKG_CONFIG_ALLOW_SYSTEM_CFLAGS=1 \
                    SETUPTOOLS_USE_DISTUTILS=stdlib
if {[string match macports-clang* ${configure.compiler}]} {
    configure.env-append \
                    "LLVM_PROFDATA=[string map {"clang" "llvm-profdata"} ${configure.cc}]"
}
build.env           SETUPTOOLS_USE_DISTUTILS=stdlib
destroot.env        SETUPTOOLS_USE_DISTUTILS=stdlib

use_parallel_build  yes

post-patch {
    reinplace "s|@@PREFIX@@|${prefix}|g" \
      ${worksrcpath}/setup.py \
      ${worksrcpath}/Lib/cgi.py \
      ${worksrcpath}/Lib/ctypes/macholib/dyld.py

    reinplace "s|/setup.py|/setup.py --no-user-cfg|" ${worksrcpath}/Makefile.pre.in

    # replace /Applications with ${applications_dir}
    reinplace "s|@@APPLICATIONS_DIR@@|${applications_dir}|" \
      ${worksrcpath}/configure
}

build.target        all

test.run            yes
test.target         test

destroot.target     frameworkinstall maninstall

# ensure that correct compiler is used
build.args-append   MAKE="${build.cmd} CC=${configure.cc}"
destroot.args-append    MAKE="${destroot.cmd} CC=${configure.cc}"

set pythonVerNoDot python[string map {. {}} $branch]
select.entries      [list python python-$pythonVerNoDot $pythonVerNoDot] \
                    [list python3 python3-$pythonVerNoDot $pythonVerNoDot]

set framewpath ${frameworks_dir}/Python.framework
set framewdir  ${framewpath}/Versions/${branch}

if {${os.platform} eq "darwin"} {
    set confdir config-${branch}-darwin

    post-configure {
        # poll() misbehaves on 10.8 and older
        # See https://trac.macports.org/ticket/18376
        if {${os.major} <= 12} {
            set oldmtime [file mtime ${worksrcpath}/pyconfig.h]
            system -W ${worksrcpath} "ed - pyconfig.h < ${filespath}/pyconfig.ed"
            file mtime ${worksrcpath}/pyconfig.h $oldmtime
        }
    }
} elseif {${os.platform} eq "linux"} {
    set confdir config-${branch}-${build_arch}-linux-gnu
} else {
    set confdir config-${branch}-${build_arch}-${os.platform}
}

configure.save_configure_cmd "install log"

post-destroot {

    platform darwin {
        foreach dir { Headers Resources Python Versions/Current } {
            file delete ${destroot}${framewpath}/${dir}
        }

        ln -s ${framewdir}/share/man/man1/python${branch}.1 ${destroot}${prefix}/share/man/man1/
        ln -s ${framewdir}/lib/pkgconfig/python-${branch}.pc ${destroot}${prefix}/lib/pkgconfig/
        ln -s ${framewdir}/lib/pkgconfig/python-${branch}-embed.pc ${destroot}${prefix}/lib/pkgconfig/

        set libdir ${destroot}${framewdir}/lib/python${branch}
        # Without this, LINKFORSHARED is set to
        # ... $(PYTHONFRAMEWORKDIR)/Versions/$(VERSION)/$(PYTHONFRAMEWORK)
        # (this becomes Python.framework/Versions/3.9/Python) which doesn't
        # work for dependents that incorrectly use this variable to find out
        # how to link against python (see ticket #15099); instead we mirror
        # the behavior of `python-config --ldflags` here.
        set lfs_pattern {^([[:space:]]*'LINKFORSHARED':).*}
        set lfs_replacement "\\1 '-L${framewdir}/lib/python${branch}/${confdir} -lpython${branch} -ldl -framework CoreFoundation',"
        reinplace -E s|${lfs_pattern}|${lfs_replacement}| \
            ${libdir}/_sysconfigdata__darwin_darwin.py

        # remove -arch flags from the config
        reinplace -E {s|-arch [a-z0-9_]+||g} \
            ${libdir}/_sysconfigdata__darwin_darwin.py

        # also remove gettext overlinking
        reinplace "s|-lintl||" \
            ${libdir}/_sysconfigdata__darwin_darwin.py

        # recompile the modified file
        set python_for_build python.exe
        # executable differs depending on filesystem case sensitivity
        if {![file exists ${worksrcpath}/${python_for_build}]} {
            set python_for_build python
        }
        system -W ${worksrcpath} "env DYLD_FRAMEWORK_PATH=. ./${python_for_build} -E -m compileall -d [shellescape ${framewdir}/lib/python${branch}] -o 0 -o 1 -o 2 [shellescape ${libdir}/_sysconfigdata__darwin_darwin.py]"

        # Also make the sysconfig changes in the Makefile
        reinplace {s|^\(LINKFORSHARED=\).*$|\1 -L$(LIBPL) -lpython$(VERSION)$(ABIFLAGS) $(LIBS) $(SYSLIBS)|} \
            ${libdir}/${confdir}/Makefile

        reinplace -E {s|-arch [a-z0-9_]+||g} \
           ${libdir}/${confdir}/Makefile

        reinplace "s|-lintl||" \
           ${libdir}/${confdir}/Makefile
    }

    foreach unversioned {2to3 idle3 pydoc3 python3 python3-config} {
        delete ${destroot}${prefix}/bin/${unversioned}
    }
    platform linux {
        xinstall -m 755 -d ${destroot}${framewdir}/lib
        ln -s ../../../../../../lib/python${branch} ${destroot}${framewdir}/lib/python${branch}
        xinstall -m 755 -d ${destroot}${framewdir}/bin
        foreach b [glob -nocomplain ${destroot}${prefix}/bin/*] {
            ln -s ${prefix}/bin/[file tail ${b}] ${destroot}${framewdir}/bin
        }
        system -W ${destroot}${prefix} "find lib -name \"*.pyc\" | xargs rm"
        foreach b {lib/libpython3.so lib/pkgconfig/python3.pc lib/pkgconfig/python3-embed.pc share/man/man1/python3.1} {
            file delete ${destroot}${prefix}/${b}
        }

        reinplace "s|python${version}\$|python${version}.gz|g" \
           ${destroot}${prefix}/etc/select/python/python39 \
           ${destroot}${prefix}/etc/select/python3/python39

        if {[file exists ${prefix}/lib/libuuid.so.1] &&
            [file exists ${destroot}${prefix}/lib/python${branch}/lib-dynload/_uuid.cpython-39-${build_arch}-linux-gnu_failed.so]
        } {
            # the build didn't pick up our libuuid.so; fix that.
            ln -s _uuid.cpython-39-${build_arch}-linux-gnu_failed.so \
                ${destroot}${prefix}/lib/python${branch}/lib-dynload/_uuid.cpython-39-${build_arch}-linux-gnu.so
            system "patchelf --add-needed libuuid.so.1 \
                ${destroot}${prefix}/lib/python${branch}/lib-dynload/_uuid.cpython-39-${build_arch}-linux-gnu.so"
        }
    }
}

notes "
To make this the default Python or Python 3 (i.e., the version run by\
the 'python' or 'python3' commands), run one or both of:

    sudo port select --set python $pythonVerNoDot
    sudo port select --set python3 $pythonVerNoDot
"

platform darwin {
variant universal {
    post-patch {
        set universal_arch_flags {}
        set arch_run_32bit {}
        set lipo_32bit_flags {}
        set lipo_intel64_flags {}
        set any64 no
        foreach arch ${configure.universal_archs} {
            lappend universal_arch_flags -arch ${arch}
            if {${arch} in {i386 ppc}} {
                lappend arch_run_32bit -${arch}
                lappend lipo_32bit_flags -extract ${arch}
            } else {
                set any64 yes
            }
        }
        if {$any64} {
            if {$arch_run_32bit eq ""} {
                set arch_run_32bit true
                set lipo_32bit_flags ""
            } else {
                set arch_run_32bit "/usr/bin/arch $arch_run_32bit"
                #lipo_32bit_flags already correct
            }
            if {"arm64" in ${configure.universal_archs} && "x86_64" in ${configure.universal_archs}} {
                set lipo_intel64_flags "-extract x86_64"
            }
        } else {
            set arch_run_32bit ""
            set lipo_32bit_flags ""
        }
        reinplace \
            "s|@@UNIVERSAL_ARCH_FLAGS@@|${universal_arch_flags}|" \
            ${worksrcpath}/configure
        reinplace \
            "s|@@LIPO_32BIT_FLAGS@@|${lipo_32bit_flags}|" \
            ${worksrcpath}/configure
        reinplace \
            "s|@@LIPO_INTEL64_FLAGS@@|${lipo_intel64_flags}|" \
            ${worksrcpath}/configure
        reinplace \
            "s|@@ARCH_RUN_32BIT@@|${arch_run_32bit}|" \
            ${worksrcpath}/configure
    }
    configure.args-append   --enable-universalsdk=${configure.sysroot}
    post-configure {
        set oldmtime [file mtime ${worksrcpath}/pyconfig.h]
        system -W ${worksrcpath} "ed - pyconfig.h < ${filespath}/pyconfig.h-universal.ed"
        file mtime ${worksrcpath}/pyconfig.h $oldmtime
    }

    post-destroot {
        foreach unversioned {python3-32 python3-intel64} {
            delete ${destroot}${prefix}/bin/${unversioned}
        }
    }
}
}

variant readline description {Use readline instead of libedit} {
    patchfiles-delete       patch-libedit.diff
    depends_lib-append      port:readline
    depends_lib-delete      port:libedit
}
default_variants            +readline

variant optimizations description {enable expensive, stable optimizations (including PGO)} {
    configure.args-append   --enable-optimizations
}

if {${os.platform} eq "darwin"} {
    # Build failures on 10.9 and older
    if {${os.major} > 11} {
        if {${os.major} > 14 || !($universal_possible && [variant_isset universal])} {
            default_variants +LTO
        }
        if {${os.major} > 13} {
            default_variants-append +optimizations
        }
    }
} else {
    default_variants-append +LTO +optimizations
}

variant dtrace description {enable DTrace support} {
    configure.args-append   --with-dtrace
}

post-activate {
    if {![file exists "${framewdir}/lib/python${branch}/site-packages/__Previous_Pythons__.pth"]} {
        set pthname "${framewdir}/lib/python${branch}/site-packages/__Previous_Pythons__.pth"
        if {![catch {set fp [open ${pthname} "w"]}]} {
            set minor [expr [lindex [split ${branch} .] 1] -1]
            set curdir [pwd]
            while {${minor} >= 0} {
                set v "3.${minor}"
                set spdir ${framewpath}/Versions/${v}/lib/python${v}/site-packages
                # just test if we have a directory by chdir'ing into it, and back
                if {![catch {_cd ${spdir}} err]} {
                    puts ${fp} "${spdir}"
                    _cd ${curdir}
                }
                incr minor -1
            }
            close ${fp}
        }
        notes-append "A file ${pthname} has been created that tells python${branch} where to find packages installed for older 3.x versions."
    }
}

livecheck.type      regex
livecheck.url       ${homepage}downloads/source/
livecheck.regex     Python (${branch}\[.0-9\]+) -
