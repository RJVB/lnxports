PortSystem              1.0
PortGroup               compiler_blacklist_versions 1.0

name                    libcxx
epoch                   1
version                 6.0.0
categories              lang
platforms               darwin
license                 MIT NCSA
maintainers             {jeremyhu @jeremyhu}
description             libc++ is a new implementation of the C++ standard library with support for C++11 and portions of C++14.
long_description        ${description} \
                        Because objects cannot be passed between different versions of the C++ runtime, this port must \
                        replace the host version in order to be used.  On Snow Leopard and earlier, this is done \
                        automatically because there is no existing host version of this library.  On Lion and later, \
                        users must build the port with +replacemnt_libcxx and install the root manually if they wish \
                        to replace the existing host implementation.

homepage                http://libcxx.llvm.org/

master_sites            http://www.llvm.org/releases/${version}/
dist_subdir             llvm

use_xz                  yes
distname                ${name}-${version}.src

checksums               rmd160  5d311ab0546181cf3084c520826786a7a9681a95 \
                        sha256  70931a87bde9d358af6cb7869e7535ec6b015f7e6df64def6d2ecdd954040dd9

if {${os.platform} eq "linux"} {
    PortGroup           cmake 1.1
    distfiles-append    llvm-${version}.src${extract.suffix}
    worksrcdir          llvm-${version}.src
    checksums-prepend   ${name}-${version}.src${extract.suffix}
    checksums-append    llvm-${version}.src${extract.suffix} \
                        rmd160  3cffa52b8842dd5858caa4cf7a998059486e462c \
                        sha256  1ff53c915b4e761ef400b803f07261ade637b0c269d99569f18040f3dcee4408
    depends_lib         port:libedit port:libffi port:ncurses port:zlib

    post-extract {
        file rename ${workpath}/${name}-${version}.src ${worksrcpath}/projects/libcxx
    }

    patch.pre_args -p1
    patchfiles-append \
        3004-Revert-Remove-the-buildit-and-testit-scripts-they-ha.patch \
        3001-Fix-local-and-iterator-when-building-with-Lion-and-n.patch \
        3003-implement-atomic-using-mutex-lock_guard-for-64b-ops-.patch \
        patch-accept-build_types.diff

    cmake.install_prefix \
                        /usr
    long_description-append \
                        On Linux, this installs libc++ to ${cmake.install_prefix} \
                        regardless of where MacPorts is installed.

    if {[file exists ${filespath}/find_abi_headers.sh]} {
        set abi_header_dirs [exec ${filespath}/find_abi_headers.sh ${configure.compiler}]
    } else {
        # we're probably executed from the registry, e.g. while deactivating the port
        set abi_header_dirs {}
    }

    configure.args-append \
                        -DLLVM_PATH=${worksrcpath} \
                        -DLLVM_TARGETS_TO_BUILD="X86\;AMDGPU" \
                        -DLIBCXX_CXX_ABI=libstdc++ \
                        -DLIBCXX_CXX_ABI_INCLUDE_PATHS="${abi_header_dirs}" \
                        -DLIBCXX_ENABLE_ASSERTIONS=OFF \
                        -DLIBCXX_BENCHMARK_NATIVE_STDLIB=libstdc++

    cmake.save_configure_cmd "log too"

    variant benchmark_libcxx description {build the benchmark against libc++} {
        configure.args-replace \
                        -DLIBCXX_BENCHMARK_NATIVE_STDLIB=libstdc++ \
                        -DLIBCXX_BENCHMARK_NATIVE_STDLIB=libc++
    }
    variant static_abi description {link the ABI library (libstdc++) statically} {
        configure.args-append \
                        -DLIBCXX_ENABLE_STATIC_ABI_LIBRARY=ON
    }

    build.dir           ${configure.dir}/projects/libcxx
    destroot.violate_mtree yes

    test.run            yes
    test.dir            ${configure.dir}
    test.target         check-libcxx

    return
}

variant universal {}
default_variants +universal
use_configure no

set roots_path ${prefix}/var/system_roots/
set root_name  ${name}-${version}-${revision}

set libcxxabi_worksrcpath   [file join ${workpath} ${libcxxabi_distname}]
set libcxx_worksrcpath      [file join ${workpath} ${libcxx_distname}]

platform darwin {
    if {${os.major} < 9} {
        pre-fetch {
            ui_error "${name} is not supported on Tiger or earlier."
            error "unsupported platform"
        }
    }

    if {${os.major} < 11} {
        post-activate {
            set dirs /
            if {${os.subplatform} eq "macosx"} {
                lappend dirs ${developer_dir}/SDKs/MacOSX${macosx_version}.sdk
            }
            foreach d ${dirs} {
                system -W ${d} "tar xzf ${roots_path}/${root_name}.tgz"
            }
        }
    }
}

variant replacemnt_libcxx description {EXPERTS ONLY: Build a replacement libcxxabi and libcxx even if it is already part of the base OS.} {}
variant runtime description "Install the libcxx library so that it can be used with DYLD_INSERT_LIBRARIES" {
    depends_build-append \
                        port:cctools
    notes-append "Start applications via ${prefix}/bin/optlibs to use the latest libc++, or\
         set DYLD_INSERT_LIBRARIES as illustrated in that script."
}


if {${os.major} < 11 || [variant_isset replacemnt_libcxx] || [variant_isset runtime]} {
    compiler.blacklist *gcc* {clang < 500}

    # clang 3.5 and newer are conditionally blacklisted to prevent dependency cycles
    foreach ver {3.5 3.6 3.7 3.8 3.9 4.0 5.0 6.0 devel} {
        if {![file exists ${prefix}/bin/clang-mp-${ver}]} {
            compiler.blacklist-append macports-clang-${ver}
        }
    }

    # clang 3.8 and newer are blacklisted due to a build failure in stdlib.h that needs to ne triaged
    foreach ver {3.8 3.9 4.0 5.0 6.0 devel} {
        compiler.blacklist-append macports-clang-${ver}
    }

    supported_archs i386 x86_64 ppc

    if {[variant_isset universal] && [lsearch ${universal_archs} ppc] != -1} {
        # http://trac.macports.org/wiki/LibcxxOnOlderSystems#Leopardppc
        compiler.whitelist macports-clang-3.7 macports-clang-3.6
    }

    post-extract {
        # delete cxa_noexception.cpp as we're not using the cmake build system
        # cmake would choose either cxa_exceptions or cxa_noexceptions
        # but the buildit script just builds every file in the src directory and you can't have both
        delete ${libcxxabi_worksrcpath}/src/cxa_noexception.cpp

        # cmake doesn't include this for darwin builds, so nuke it here as well
        delete ${libcxxabi_worksrcpath}/src/cxa_thread_atexit.cpp

        file mkdir ${workpath}/build
        file copy ${filespath}/Makefile ${workpath}/build
    }

    patch.dir ${workpath}
    patch.pre_args -p0
    patchfiles-append \
        0001-Update-buildit-script-to-read-EXTRA_LDFLAGS-from-the.patch \
        0002-Define-_LIBCPP_BUILDING_LIBRARY-during-build-to-supp.patch \
        0003-buildit-Use-undefined-dynamic_lookup-when-linking.patch \
        3001-Fix-local-and-iterator-when-building-with-Lion-and-n.patch \
        3002-Fix-missing-long-long-math-prototypes-when-using-the.patch \
        3003-implement-atomic-using-mutex-lock_guard-for-64b-ops-.patch \
        3004-Revert-Remove-the-buildit-and-testit-scripts-they-ha.patch \
        3005-buildit-build-fix-for-Leopard.patch \
        3006-buildit-Set-compatibility-version-to-RC_ProjectSourc.patch \
        3007-MacPorts-Use-LIBCXXABI_PATH-for-path-to-build-time-l.patch \
        3008-buildit-Fix-CFLAGS-for-Leopard-and-Snow-Leopard-to-i.patch

    post-patch {
        system "chmod 755 ${libcxx_worksrcpath}/lib/buildit"
    }

    build.dir ${workpath}/build
    build.env-append \
        CC="${configure.cc}" \
        CXX="${configure.cxx}" \
        RC_XBS=1 \
        RC_ProjectSourceVersion="${version}" \
        RC_CFLAGS="[get_canonical_archflags] -I${libcxxabi_worksrcpath}/include -I${libcxx_worksrcpath}/include ${configure.optflags}" \
        TRIPLE="-apple-darwin${os.major}"
    build.args-append \
        LIBCXXABI_PATH=${libcxxabi_worksrcpath} \
        LIBCXX_PATH=${libcxx_worksrcpath} \
        LIBCXXABI_DYLIB_PATH=${build.dir}/libc++abi.dylib

    # We want to use the host's libc++ everywhere because we want to ensure
    # that only *ONE* copy of this library is in any process's address space.
    # On Snow Leopard and earlier, this port provides this library in /usr for
    # binary compatibility with Lion and later.
    #
    # On newer OS version, this port does not install the content to /usr and
    # just leaves it to expert users who may want to replace their system
    # runtime.

    destroot {
        if {[variant_isset replacemnt_libcxx]} {
            xinstall -m 755 -d ${destroot}${roots_path}/${root_name}/usr/lib

            xinstall -m 755 ${libcxxabi_worksrcpath}/lib/libc++abi.dylib ${destroot}${roots_path}/${root_name}/usr/lib

            xinstall -m 755 ${libcxx_worksrcpath}/lib/libc++.1.dylib ${destroot}${roots_path}/${root_name}/usr/lib
            ln -s libc++.1.dylib ${destroot}${roots_path}/${root_name}/usr/lib/libc++.dylib

            system -W ${destroot}${roots_path}/${root_name} "tar czf ../${root_name}.tgz ."
            delete ${destroot}${roots_path}/${root_name}
        }
        if {[variant_isset runtime]} {
            # install only the runtime libraries so they will not be picked up by the linker/editor!
            xinstall -m 755 ${libcxxabi_worksrcpath}/lib/libc++abi.dylib ${destroot}${prefix}/lib/libc++abi.1.dylib
            xinstall -m 755 ${libcxx_worksrcpath}/lib/libc++.1.dylib ${destroot}${prefix}/lib/
            system "install_name_tool -id ${prefix}/lib/libc++abi.1.dylib ${destroot}${prefix}/lib/libc++abi.1.dylib"
            system "install_name_tool -id ${prefix}/lib/libc++.1.dylib ${destroot}${prefix}/lib/libc++.1.dylib"
            system "install_name_tool -change /usr/lib/libc++abi.dylib ${prefix}/lib/libc++abi.1.dylib ${destroot}${prefix}/lib/libc++.1.dylib"
            xinstall -m 755 ${filespath}/optlibs.sh ${destroot}${prefix}/bin/optlibs
            reinplace "s|@PREFIX@|${prefix}|g" ${destroot}${prefix}/bin/optlibs
        }
    }
} else {
    distfiles
    fetch {}
    build {}
    destroot {
        xinstall -d ${destroot}${prefix}/share/doc/${name}
        system "echo ${name} is an empty port on this OS version because the functionality is already provided by the OS. > ${destroot}${prefix}/share/doc/${name}/README.txt"
    }
}

livecheck.type none
