# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem 1.0
PortGroup clang_dependency 1.0
PortGroup openssl 1.0

if {${os.platform} eq "darwin"} {
    set LTO.configure_option --with-lto=full
} else {
    set LTO.configure_option --with-lto
}
platform darwin {
    set LTO.allow_UseLLD no
}
PortGroup LTO 1.0
PortGroup save_configure_cmd 1.0

name                python313

# Remember to keep py313-tkinter and py313-gdbm's versions sync'd with this
version             3.13.9

set branch          [join [lrange [split ${version} .] 0 1] .]
categories          lang
license             PSF
maintainers         {jmr @jmroot}

description         An interpreted, object-oriented programming language
long_description    Python is an interpreted, interactive, object-oriented \
                    programming language.

homepage            https://www.python.org/
master_sites        ${homepage}ftp/python/${version}/

platforms           linux darwin

distname            Python-${version}
use_xz              yes
checksums           md5 516aabdf3de01eeefb6de1aacf9df810 \
                    rmd160 19d545e3fb13c7d8e83833f6870653c668c6c725 \
                    sha256 ed5ef34cda36cfa2f3a340f07cac7e7814f91c7f3c411f6d3562323a866c5c66

patchfiles          patch-configure.diff \
                    Makefile.pre.in.patch \
                    patch-Lib-ctypes-macholib-dyld.py.diff \
                    configure-disable-tkinter.patch \
                    configure-disable-system-libffi.patch

if {${os.platform} eq "darwin"} {
    patchfiles-append \
                    configure-disable-libb2.patch \
                    configure-disable-libuuid.patch
}
platform linux {
    # disable tests that have been known to fail (on ZFS?)
    patchfiles-append \
                    patch-test_sqlite3.diff
    depends_build-append \
                    port:ncurses-dev \
                    port:gettext-dev \
                    port:ncurses-dev \
                    port:patchelf
    depends_lib-append \
                    port:libb2 \
                    path:lib/libuuid.so:util-linux
}

depends_build-append \
                    path:bin/pkg-config:pkgconfig

depends_lib-append  port:bzip2 \
                    port:expat \
                    port:gettext-runtime \
                    port:libedit \
                    port:libffi \
                    port:mpdecimal \
                    port:ncurses \
                    port:sqlite3 \
                    port:xz \
                    port:zlib

set pythonVerNoDot  [string map {. {}} $branch]
if {$subport ne "${name}-freethreading"} {
    depends_run     port:python_select-${pythonVerNoDot} \
                    port:python3_select-${pythonVerNoDot}
}

# blacklist llvm-gcc-4.2 compiler known to produce bad code
compiler.blacklist-append *llvm-gcc-4.2
if {${os.platform} ne "darwin"} {
    # to prevent `conflicts_build     ${subport} py39-setuptools` :
    configure.cppflags-delete -I${prefix}/include
}

compiler.c_standard 2011

configure.args-append \
                    [expr {${os.platform} eq "darwin" ? "--enable-framework=${frameworks_dir}" : "--enable-shared"}] \
                    --enable-ipv6 \
                    --enable-loadable-sqlite-extensions \
                    --with-computed-gotos \
                    --with-ensurepip=no \
                    --with-readline=editline \
                    --with-system-expat \
                    --with-dbmliborder=ndbm:bdb \
                    --enable-experimental-jit=no

# configure.ccache    no
# pkg-config removes -I flags for paths in CPATH, which confuses python.
configure.env       PKG_CONFIG_ALLOW_SYSTEM_CFLAGS=1
if {[string match macports-clang* ${configure.compiler}]} {
    configure.env-append \
                    "LLVM_PROFDATA=[string map {"clang" "llvm-profdata"} ${configure.cc}]" \
                    "LLVM_SYMBOLIZER_PATH=[string map {"clang" "llvm-symbolizer"} ${configure.cc}]"
    build.env-append \
                    "LLVM_PROFDATA=[string map {"clang" "llvm-profdata"} ${configure.cc}]" \
                    "LLVM_SYMBOLIZER_PATH=[string map {"clang" "llvm-symbolizer"} ${configure.cc}]"
}
use_parallel_build  yes

post-patch {
    reinplace "s|@@PREFIX@@|${prefix}|g" \
      ${worksrcpath}/Lib/ctypes/macholib/dyld.py

    # replace /Applications with ${applications_dir}
    reinplace "s|@@APPLICATIONS_DIR@@|${applications_dir}|" \
      ${worksrcpath}/configure
    reinplace "s|@@ABIFLAGS@@|${abiflags}|" \
      ${worksrcpath}/configure
}

build.target        all

test.run            yes
test.target         test

destroot.target     frameworkinstall maninstall

# ensure that correct compiler is used
build.args-append   MAKE="${build.cmd} CC=${configure.cc}"
destroot.args-append    MAKE="${destroot.cmd} CC=${configure.cc}"
subport ${name}-freethreading {
    description-append  (free threading)
    long_description-append This port has the experimental free threading\
                        feature enabled, i.e. there is no global interpreter\
                        lock (GIL).
    conflicts           ${name}
}
variant freethreading description {Enable the experimental free threading feature} {}

if {$subport ne "${name}-freethreading"} {
    conflicts       ${name}-freethreading
    set abiflags    {}
}

if {$subport eq "${name}-freethreading" || [variant_isset freethreading]} {
    notes-append "Python with free threading has a different ABI and is thus\
            incompatible with extension modules built for the standard ABI."
    # Needs mimalloc which needs arc4random_buf
    platforms-replace   darwin {darwin >= 11}
    # Needs stdatomic.h
    compiler.blacklist  {clang < 700}
    set abiflags        t
    configure.args-append --disable-gil
#     post-destroot {
#         if {[file exists "${destroot}${applications_dir}/Python ${branch}"]} {
#             move    "${destroot}${applications_dir}/Python ${branch}" \
#                     "${destroot}${applications_dir}/Python ${branch}${abiflags}"
#         }
#         foreach exe [list idle${branch} pydoc${branch} python${branch} python${branch}-config] {
#             delete ${destroot}${prefix}/bin/${exe}
#         }
#     }
}

platform darwin {
    if {${os.major} < 11} {
        configure.args-append   --without-mimalloc
    }
    if {[vercmp $macosx_deployment_target <= 10.5]} {
        patchfiles-append  patch-threadid-older-systems.diff
        configure.cppflags-append -D_DARWIN_USE_64_BIT_INODE
    }

    post-configure {
        # poll() misbehaves on 10.8 and older
        # See https://trac.macports.org/ticket/18376
        if {${os.major} <= 12} {
            set oldmtime [file mtime ${worksrcpath}/pyconfig.h]
            system -W ${worksrcpath} "ed - pyconfig.h < ${filespath}/pyconfig.ed"
            file mtime ${worksrcpath}/pyconfig.h $oldmtime
        }
    }
}

set framewpath  ${frameworks_dir}/Python.framework
set framewdir   ${framewpath}/Versions/${branch}${abiflags}

post-destroot {

    if {${os.platform} eq "darwin"} {
        set confdir config-${branch}${abiflags}-darwin

        foreach dir { Headers Resources Python Versions/Current } {
            file delete ${destroot}${framewpath}/${dir}
        }

        if {$subport ne "${name}-freethreading"} {
            ln -s ${framewdir}/share/man/man1/python${branch}.1 ${destroot}${prefix}/share/man/man1/
        }
        ln -s ${framewdir}/lib/pkgconfig/python-${branch}${abiflags}.pc ${destroot}${prefix}/lib/pkgconfig/
        ln -s ${framewdir}/lib/pkgconfig/python-${branch}${abiflags}-embed.pc ${destroot}${prefix}/lib/pkgconfig/

        set libdir ${destroot}${framewdir}/lib/python${branch}${abiflags}
        # Without this, LINKFORSHARED is set to
        # ... $(PYTHONFRAMEWORKDIR)/Versions/$(VERSION)/$(PYTHONFRAMEWORK)
        # (this becomes Python.framework/Versions/3.13/Python) which doesn't
        # work for dependents that incorrectly use this variable to find out
        # how to link against python (see ticket #15099); instead we mirror
        # the behavior of `python-config --ldflags` here.
        set lfs_pattern {^([[:space:]]*'LINKFORSHARED':).*}
        set lfs_replacement "\\1 '-L${framewdir}/lib/python${branch}/${confdir} -lpython${branch} -ldl -framework CoreFoundation',"
        reinplace -E s|${lfs_pattern}|${lfs_replacement}| \
            ${libdir}/_sysconfigdata_${abiflags}_darwin_darwin.py

        # remove -arch flags from the config
        reinplace -E {s|-arch [a-z0-9_]+||g} \
            ${libdir}/_sysconfigdata_${abiflags}_darwin_darwin.py

        # also remove gettext overlinking
        reinplace "s|-lintl||" \
            ${libdir}/_sysconfigdata_${abiflags}_darwin_darwin.py

        reinplace "s|ccache ||" \
            ${libdir}/_sysconfigdata_${abiflags}_darwin_darwin.py

        # recompile the modified file
        set python_for_build python.exe
        # executable differs depending on filesystem case sensitivity
        if {![file exists ${worksrcpath}/${python_for_build}]} {
            set python_for_build python
        }
        system -W ${worksrcpath} "env DYLD_FRAMEWORK_PATH=. ./${python_for_build} -E -m compileall -d [shellescape ${framewdir}/lib/python${branch}] -o 0 -o 1 -o 2 [shellescape ${libdir}/_sysconfigdata_${abiflags}_darwin_darwin.py]"

        # Also make the sysconfig changes in the Makefile
        reinplace {s|^\(LINKFORSHARED=\).*$|\1 -L$(LIBPL) -lpython$(VERSION)$(ABIFLAGS) $(LIBS) $(SYSLIBS)|} \
            ${libdir}/${confdir}/Makefile

        reinplace -E {s|-arch [a-z0-9_]+||g} \
           ${libdir}/${confdir}/Makefile

        reinplace "s|-lintl||" \
           ${libdir}/${confdir}/Makefile
    } elseif {${os.platform} eq "linux"} {
        set confdir config-${branch}${abiflags}-${build_arch}-linux-gnu
        set libdir ${destroot}${prefix}/lib/python${branch}${abiflags}
        reinplace "s|ccache ||g" \
            ${libdir}/_sysconfigdata_${abiflags}_linux_${build_arch}-linux-gnu.py

    } else {
        set confdir config-${branch}${abiflags}-${build_arch}-${os.platform}
        set libdir ${destroot}${prefix}/lib/python${branch}${abiflags}
        reinplace "s|ccache ||g" \
            ${libdir}/_sysconfigdata_${abiflags}_${os.platform}_${build_arch}-${os.platform}.py
    }

    reinplace "s|ccache ||g" ${libdir}/${confdir}/Makefile

    foreach unversioned {2to3 idle3 pydoc3 python3 python3-config} {
        delete ${destroot}${prefix}/bin/${unversioned}
    }
    platform linux {
        xinstall -m 755 -d ${destroot}${framewdir}/lib
        ln -s ../../../../../../lib/python${branch}${abiflags} ${destroot}${framewdir}/lib/python${branch}
        xinstall -m 755 -d ${destroot}${framewdir}/bin
        foreach b [glob -nocomplain ${destroot}${prefix}/bin/*] {
            ln -s ${prefix}/bin/[file tail ${b}] ${destroot}${framewdir}/bin
        }
        system -W ${destroot}${prefix} "find lib -name \"*.pyc\" | xargs rm"
        foreach b {lib/libpython3.so lib/pkgconfig/python3.pc lib/pkgconfig/python3-embed.pc share/man/man1/python3.1} {
            file delete ${destroot}${prefix}/${b}
        }

        if {[file exists ${prefix}/lib/libuuid.so.1] &&
            [file exists ${destroot}${prefix}/lib/python${branch}/lib-dynload/_uuid.cpython-${branch}-${build_arch}-linux-gnu_failed.so]
        } {
            # the build didn't pick up our libuuid.so; fix that.
            ln -s _uuid.cpython-${branch}-${build_arch}-linux-gnu_failed.so \
                ${destroot}${prefix}/lib/python${branch}/lib-dynload/_uuid.cpython-${branch}-${build_arch}-linux-gnu.so
            system "patchelf --add-needed libuuid.so.1 \
                ${destroot}${prefix}/lib/python${branch}/lib-dynload/_uuid.cpython-${branch}-${build_arch}-linux-gnu.so"
        }
    }
}

if {$subport ne "${name}-freethreading"} {
notes-append "
To make this the default Python or Python 3 (i.e., the version run by\
the 'python' or 'python3' commands), run one or both of:

    sudo port select --set python python$pythonVerNoDot
    sudo port select --set python3 python$pythonVerNoDot
"
}

platform darwin {
variant universal {
    post-patch {
        set universal_arch_flags {}
        set arch_run_32bit {}
        set lipo_32bit_flags {}
        set lipo_intel64_flags {}
        set any64 no
        foreach arch ${configure.universal_archs} {
            lappend universal_arch_flags -arch ${arch}
            if {${arch} in {i386 ppc}} {
                lappend arch_run_32bit -${arch}
                lappend lipo_32bit_flags -extract ${arch}
            } else {
                set any64 yes
            }
        }
        if {$any64} {
            if {$arch_run_32bit eq ""} {
                set arch_run_32bit true
                set lipo_32bit_flags ""
            } else {
                set arch_run_32bit "/usr/bin/arch $arch_run_32bit"
                #lipo_32bit_flags already correct
            }
            if {"arm64" in ${configure.universal_archs} && "x86_64" in ${configure.universal_archs}} {
                set lipo_intel64_flags "-extract x86_64"
            }
        } else {
            set arch_run_32bit ""
            set lipo_32bit_flags ""
        }
        reinplace \
            "s|@@UNIVERSAL_ARCH_FLAGS@@|${universal_arch_flags}|" \
            ${worksrcpath}/configure
        reinplace \
            "s|@@LIPO_32BIT_FLAGS@@|${lipo_32bit_flags}|" \
            ${worksrcpath}/configure
        reinplace \
            "s|@@LIPO_INTEL64_FLAGS@@|${lipo_intel64_flags}|" \
            ${worksrcpath}/configure
        reinplace \
            "s|@@ARCH_RUN_32BIT@@|${arch_run_32bit}|" \
            ${worksrcpath}/configure
    }
    configure.args-append   --enable-universalsdk=${configure.sysroot}
    post-configure {
        set oldmtime [file mtime ${worksrcpath}/pyconfig.h]
        system -W ${worksrcpath} "ed - pyconfig.h < ${filespath}/pyconfig.h-universal.ed"
        file mtime ${worksrcpath}/pyconfig.h $oldmtime
    }

    post-destroot {
        foreach unversioned {python3-32 python3-intel64} {
            delete ${destroot}${prefix}/bin/${unversioned}
        }
    }
}
}

variant readline description {Use readline instead of libedit} {
    configure.args-replace  --with-readline=editline \
                            --with-readline=readline
    depends_lib-append      port:readline
    depends_lib-delete      port:libedit
}
if {${os.platform} ne "darwin"} {
    default_variants        +readline
}

variant optimizations description {enable expensive, stable optimisations (including PGO)} {
    configure.args-append   --enable-optimizations
}

if {${os.platform} eq "darwin"} {
    # Build failures on 10.10 and older (with LTO, symbol visibility issues arise)
    if {${os.major} > 11} {
        if {${os.major} > 14 || ((${os.major} == 14 && $subport eq $name)
            && !($universal_possible && [variant_isset universal]) && ![variant_isset freethreading])
        } then {
            default_variants-append +LTO
        }
        if {${os.major} > 14} {
            # this actually just requires a recent enough (clang) compiler
            # clang-9.0 allowed PGO for me on 10.9.5
            default_variants-append +optimizations
        }
    }
} else {
    default_variants-append +LTO +optimizations
}

variant dtrace description {enable DTrace support} {
    configure.args-append   --with-dtrace
}

variant jit description {enable the experimental JIT compiler} {
    depends_lib-append      port:clang-18
    configure.args-replace  --enable-experimental-jit=no \
                            --enable-experimental-jit=yes
}

configure.save_configure_cmd "install log"

if {${subport} ne "${name}-freethreading" && ![variant_isset freethreading]} {
    post-activate {
        if {![file exists "${framewdir}/lib/python${branch}${abiflags}/site-packages/__Previous_Pythons__.pth"]} {
            set pthname "${framewdir}/lib/python${branch}${abiflags}/site-packages/__Previous_Pythons__.pth"
            if {![catch {set fp [open ${pthname} "w"]}]} {
                set minor [expr [lindex [split ${branch} .] 1] -1]
                set curdir [pwd]
                while {${minor} >= 0} {
                    set v "3.${minor}"
                    set spdir ${framewpath}/Versions/${v}/lib/python${v}/site-packages
                    # just test if we have a directory by chdir'ing into it, and back
                    if {![catch {_cd ${spdir}} err]} {
                        puts ${fp} "${spdir}"
                        _cd ${curdir}
                    }
                    incr minor -1
                }
                close ${fp}
                notes-append "A file ${pthname} has been created that tells python${branch} where to find packages installed for older 3.x versions."
            }
        }
    }
}

build.post_args     -wk

livecheck.type      regex
livecheck.url       ${homepage}downloads/source/
livecheck.regex     Python (${branch}\[.0-9\]+) -
